# Maintainer: Santiago Lo Coco <mail at slococo dot com dot ar>

_pkgname="cdmenu"
pkgname="${_pkgname}-git"
pkgver=5.1.r1.g57a911c
pkgrel=1
pkgdesc="Heavily-patched build of dmenu."
arch=('x86_64')
url="https://github.com/santilococo/cdmenu"
license=('MIT')
depends=("libxft-bgra" "libxinerama")
makedepends=("git")
provides=('dmenu')
conflicts=('dmenu')
source=("git+${url}")
sha256sums=('SKIP')

pkgver() {
    cd "${_pkgname}"
    git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
    cd "${_pkgname}"
    make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
    cd "${_pkgname}"
    make PREFIX=/usr DESTDIR="${pkgdir}" install
    install -d -m755 "${pkgdir}/usr/share/licenses/${_pkgname}"
    install -m644 LICENSE "${pkgdir}/usr/share/licenses/${_pkgname}/"
}
