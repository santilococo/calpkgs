# Maintainer: Santiago Lo Coco <santilococo.01 at gmail dot com>

_pkgname="zaread"
pkgname="${_pkgname}-git"
pkgver=r13.4a59a78
pkgrel=4
pkgdesc="A (very) lightweight MS Office file reader."
arch=("any")
url="https://github.com/paoloap/zaread"
license=("GPL")
depends=("bash")
makedepends=("git")
provides=("zaread")
conflicts=("zaread")
source=("git+${url}.git" 
        "markdown_ebook_cache.patch")
sha256sums=('SKIP'
            'de08cd9a3212405ec95557b648ad222bd5db49fb55339fd5ffa10c2df153ec3a')

pkgver() {
    cd "${srcdir}/${_pkgname}"
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
    cd "${srcdir}/${_pkgname}"
    patch -p1 < "${srcdir}/markdown_ebook_cache.patch"
}

package() {
    cd "${srcdir}/${_pkgname}"
    install -Dm755 "${_pkgname}" "${pkgdir}/usr/bin/${_pkgname}"
    install -Dm644 "LICENSE" "${pkgdir}/usr/share/licenses/${_pkgname}/LICENSE"
}
