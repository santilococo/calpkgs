# Maintainer: Santiago Lo Coco <mail at slococo dot com dot ar>

_pkgname="passhole"
pkgname="${_pkgname}-git"
pkgver=1
pkgrel=1
pkgdesc="KeePass CLI + dmenu interface"
arch=("any")
url="https://gitlab.com/slococo/passhole"
license=("GPL")
_pydeps=("pynput" "pykeepass" "colorama" "pyotp")
depends=("python" ${_pydeps[@]/#/python-})
makedepends=("python-setuptools" "git")
provides=("passhole")
conflicts=("passhole")
source=("git+${url}.git")
sha256sums=('SKIP')

pkgver() {
    cd "${srcdir}/${_pkgname}"
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

package() {
    cd "${srcdir}/${_pkgname}"
    python setup.py install --root="$pkgdir/" --optimize=1
    install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${_pkgname}/LICENSE"
}
