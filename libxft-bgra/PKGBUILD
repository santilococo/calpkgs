# Maintainer: Santiago Lo Coco <santilococo.01@gmail.com>

pkgname=libxft-bgra
pkgver=2.3.4
pkgrel=1
pkgdesc="X FreeType library with support for BGRA glyphs and scaling"
arch=('x86_64')
url="https://gitlab.freedesktop.org/xorg/lib/libxft"
license=('custom')
provides=('libxft')
conflicts=('libxft')
depends=('fontconfig' 'libxrender')
makedepends=('pkgconf')
source=("https://xorg.freedesktop.org/releases/individual/lib/libXft-${pkgver}.tar.gz"
        "https://xorg.freedesktop.org/releases/individual/lib/libXft-${pkgver}.tar.gz.sig"
        "bgra-patch::${url}/merge_requests/1.patch")
sha256sums=('1eca71bec9cb483165ce1ab94f5cd3036269f5268651df6a2d99c4a7ab644d79' 
            'SKIP'
            '0e313ab2eccd7d2f68fc2d409493dd972de9cbe70510c4eb707ad16f8ed6f03e')
validpgpkeys=('4A193C06D35E7C670FA4EF0BA2FB9E081F2D130E')

build() {
    cd "${srcdir}/libXft-${pkgver}"
    patch -p1 < "${srcdir}/bgra-patch"
    sh configure --prefix=/usr --sysconfdir=/etc --disable-static
    make
}

package() {
    cd "${srcdir}/libXft-${pkgver}"
    make DESTDIR="${pkgdir}" install
    install -d -m755 "${pkgdir}/usr/share/licenses/${pkgname}"
    install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
