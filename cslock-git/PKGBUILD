# Maintainer: Santiago Lo Coco <mail at slococo dot com dot ar>

_pkgname="cslock"
pkgname="${_pkgname}-git"
pkgver=r142.95fd982
pkgrel=1
pkgdesc="Heavily-patched build of slock."
arch=('x86_64')
url="https://gitlab.com/slococo/cslock"
license=('MIT')
depends=("libxrandr" "libxinerama" "imlib2" "libxcrypt" "libxft")
makedepends=("git")
provides=('slock')
conflicts=('slock')
source=("git+${url}")
sha256sums=('SKIP')

pkgver() {
    cd "${_pkgname}"
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
    cd "${_pkgname}"
    make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
    cd "${_pkgname}"
    make PREFIX=/usr DESTDIR="${pkgdir}" install
    install -d -m755 "${pkgdir}/usr/share/licenses/${_pkgname}"
    install -m644 LICENSE "${pkgdir}/usr/share/licenses/${_pkgname}/"
}
